#!/usr/bin/env bpftrace
/** @file

  Print SystemTap ATS probes.

  @section license License

  Licensed to the Apache Software Foundation (ASF) under one
  or more contributor license agreements.  See the NOTICE file
  distributed with this work for additional information
  regarding copyright ownership.  The ASF licenses this file
  to you under the Apache License, Version 2.0 (the
  "License"); you may not use this file except in compliance
  with the License.  You may obtain a copy of the License at

      http://www.apache.org/licenses/LICENSE-2.0

  Unless required by applicable law or agreed to in writing, software
  distributed under the License is distributed on an "AS IS" BASIS,
  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
  See the License for the specific language governing permissions and
  limitations under the License.

 */


BEGIN
{
  @readlines = 1;
}


interval:s:1
{

// Iterate over elements in @lasttransaction map
    for ($kv : @lasttransaction)
    {
        $conn_id = $kv.0; // Get client IP (key)
        $last_txn_time = $kv.1; // Get last transaction time (value)
        $idle_threshold_ns = 60 * 1000000000; // 60 seconds in nanoseconds

        if ($last_txn_time != 0)
        {
            $time_since_last_txn = nsecs - $last_txn_time;

            // Check if connection is idle
            if ((int64)$time_since_last_txn > $idle_threshold_ns)
            {
                $client_ip = @conn_id_to_ip[$conn_id];
                // Increment idle connection count
                if ($client_ip != "")
                {
                    // Increment idle connection count
                    @idle_connections[$client_ip]++;
                    printf("Idle connection from IP: %s\n", $client_ip);
                }
            }
        }
    }
}


// Attach to the new_origin_server_connection probe
 usdt:*:new_origin_server_connection
 {
     printf("new_origin_server_connection: %s\n", str(arg0));
     @readlines++;
 }

// Attach to the start_connection_with_ip_addresses probe and count connections by client IP

usdt:*:start_connection_with_ip_addresses
 {     
    $client_ip = str(arg0);
    $conn_id = arg2;

    printf("New connection from IP: %s\n", $client_ip);
    @connections[$client_ip]++; // Count connections by client IP
    @lasttransaction[$conn_id] = nsecs; // Store last transaction time with client IP
    @conn_id_to_ip[$conn_id] = $client_ip
 }



// Print the result on exit

END
{
    printf("\nConnections by client IP:\n");
    print(@connections);
    printf("\nTotal lines read: %d\n", @readlines);
    printf("\nLast transaction stamps by client IP:\n");
    print(@lasttransaction);

    printf("\nIdle Connections:\n");
    // Print idle connections
    print(@idle_connections);

}
    